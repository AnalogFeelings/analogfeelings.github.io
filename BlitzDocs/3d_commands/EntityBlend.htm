<html>
<head>
<title>Blitz3D Docs</title>
<link rel=stylesheet href=../css/commands.css type=text/css>
</head>
<body>
<h1>EntityBlend Entity, Blend</h1>
<h1>Parameters</h1>
<table>
<tr>
<td>
Entity - Entity handle<br />
<br />
Blend - Blend mode of the entity.<br />
1: Alpha (default)<br />
2: Multiply<br />
3: Add
</td>
</tr>
</table>
<h1>Description</h1>
<table>
<tr>
<td>
Sets the blending mode of an entity. This blending mode determines the way in which the new RGBA of the pixel being rendered is combined with the RGB of the background.<br />
<br />
To calculate the new RGBA of the pixel being rendered, the texture RGBA for the pixel (see <a class=small href=../3d_commands/TextureBlend.htm>TextureBlend</a> for more information on how the texture RGBA is calculated) is taken, its alpha component multiplied by the entities/brushes (where applicable) alpha value and its color compentent multiplied by the entities/brushes colour. This is the RGBA which will then be blended into the background pixel, and how this is done depends on the EntityBlend value.<br />
<br />
Alpha:<br />
This blends the pixels according to the Alpha value. This is rougly done to the formula:<br />
<br />
Rr = ( An * Rn ) + ( ( 1.0 - An ) * Ro )<br />
Gr = ( An * Gn ) + ( ( 1.0 - An ) * Go )<br />
Br = ( An * Bn ) + ( ( 1.0 - An ) * Bo )<br />
<br />
Where R = Red, G = Green, B = Blue, n = new pixel colour values, r = resultant colour values, o = old pixel colour values.<br />
<br />
Alpha blending is the default blending mode and is used with most world objects.<br />
<br />
Multiply:<br />
This blend mode will darken the underlying pixels. If you think of each RGB value as being on a scale from 0% to 100%, where 0 = 0% and 255 = 100%, the multiply blend mode will multiply the red, green and blue values individually together in order to get the new RGB value, roughly according to:<br />
<br />
Rr = ( ( Rn / 255.0 ) * ( Ro / 255.0 ) ) * 255.0<br />
Gr = ( ( Gn / 255.0 ) * ( Go / 255.0 ) ) * 255.0<br />
Br = ( ( Bn / 255.0 ) * ( Bo / 255.0 ) ) * 255.0<br />
<br />
The alpha value has no effect with multiplicative blending. Blending a RGB value of 255, 255, 255 will make no difference, while an RGB value of 128, 128, 128 will darken the pixels by a factor of 2 and an RGB value of 0, 0, 0 will completely blacken out the resultant pixels. An RGB value of 0, 255, 255 will remove the red component of the underlying pixel while leaving the other color values <br />
untouched.<br />
<br />
Multiply blending is most often used for lightmaps, shadows or anything else that needs to 'darken' the resultant pixels.<br />
<br />
Add: <br />
Additive blending will add the new color values to the old, roughly according to:<br />
<br />
Rr = ( Rn * An ) + Ro<br />
Gr = ( Gn * An ) + Go<br />
Br = ( Bn * An ) + Bo<br />
<br />
The resultant RGB values are clipped out at 255, meaning that multiple additive effects can quickly cause visible banding from smooth gradients.<br />
<br />
Additive blending is extremely useful for effects such as laser shots and fire.
<br>
<br>
See also: <a class=small href=TextureBlend.htm>TextureBlend</a>, <a class=small href=EntityAlpha.htm>EntityAlpha</a>.
</td>
</tr>
</table>
<h1><a href=../3d_examples/EntityBlend.bb>Example</a></h1>
<table>
<tr>
<td>
Graphics3D 640,480<br />
<br />
SetBuffer BackBuffer()<br />
<br />
SeedRnd MilliSecs()<br />
<br />
<br />
; create camera<br />
camera=CreateCamera()<br />
CameraClsColor camera,160,160,160<br />
PositionEntity camera,0,0,-30<br />
middle=CreatePivot()<br />
EntityParent camera,middle<br />
<br />
; create add texture - white cirlce on a black background<br />
For n=0 To 50<br />
Color 5+(n*5),5+(n*5),5+(n*5)<br />
Oval 10+n,10+n,236-(n*2),236-(n*2),1<br />
Next<br />
<br />
blob_tex=CreateTexture(256,256)<br />
blob=CreateImage(256,256)<br />
GrabImage blob,0,0<br />
CopyRect 0,0,256,256,0,0,ImageBuffer(blob),TextureBuffer(blob_tex)<br />
FreeImage blob<br />
<br />
max_blobs=100<br />
<br />
; create blobs using add blend mode<br />
Dim blobs(max_blobs) ; blob sprites<br />
Dim xyblobs#(max_blobs,2) ; blob vector<br />
<br />
For n=0 To max_blobs<br />
	blobs(n)=CreateSprite()<br />
	EntityFX blobs(n),1<br />
	EntityBlend blobs(n),3 ;set blend mode to add<br />
	EntityTexture blobs(n),blob_tex<br />
	xyblobs(n,0)=Rnd(-.1,.1)<br />
	xyblobs(n,1)=Rnd(-.1,.1)<br />
	xyblobs(n,2)=Rnd(-.1,.1)<br />
	EntityColor blobs(n),Rand(0,255),Rand(0,255),Rand(0,255) ;give it a colour<br />
	Next<br />
<br />
; create cube texture<br />
Color 255,255,255<br />
Rect 0,0,256,256,1<br />
For n=0 To 7<br />
	If n=0 Then Color 0,0,0<br />
	If n=1 Then Color 0,0,255<br />
	If n=2 Then Color 0,255,0<br />
	If n=3 Then Color 0,255,255<br />
	If n=4 Then Color 255,0,0<br />
	If n=5 Then Color 255,0,255<br />
	If n=6 Then Color 255,255,0<br />
	If n=7 Then Color 255,255,255<br />
	Rect n*32,n*32,32,32,1<br />
	Next<br />
Color 0,0,0<br />
For n=0 To 255 Step 32<br />
Line 0,n,255,n<br />
Line n,0,n,255<br />
Next<br />
<br />
cube_tex=CreateTexture(256,256)<br />
cube=CreateImage(256,256)<br />
GrabImage cube,0,0<br />
CopyRect 0,0,256,256,0,0,ImageBuffer(cube),TextureBuffer(cube_tex)<br />
FreeImage cube<br />
<br />
; create cube<br />
cube=CreateCube()<br />
ScaleEntity cube,11,11,11<br />
EntityTexture cube,cube_tex<br />
EntityFX cube,17 ;set fullbright and 2 sided textures<br />
EntityBlend cube,2 ;set multiply blend<br />
<br />
Repeat<br />
<br />
; move the blobs around<br />
For n=0 To max_blobs<br />
	MoveEntity blobs(n),xyblobs(n,0),xyblobs(n,1),xyblobs(n,2)<br />
	;bounce off sides<br />
	If EntityX(blobs(n))<-10 Or EntityX(blobs(n))>10 Then xyblobs(n,0)=-xyblobs(n,0)<br />
	If EntityY(blobs(n))<-10 Or EntityY(blobs(n))>10 Then xyblobs(n,1)=-xyblobs(n,1)<br />
	If EntityZ(blobs(n))<-10 Or EntityZ(blobs(n))>10 Then xyblobs(n,2)=-xyblobs(n,2)<br />
	Next<br />
<br />
; turn camera<br />
TurnEntity middle,.1,.2,.3<br />
<br />
<br />
UpdateWorld<br />
RenderWorld<br />
Flip<br />
<br />
<br />
Until KeyHit(1)
</td>
</tr>
</table>
<br>
<a target=_top href=../index.htm>Index</a><br>
<br>
Click <a href=http://www.blitzbasic.co.nz/b3ddocs/command.php?name=EntityBlend&ref=comments target=_blank>here</a> to view the latest version of this page online</body>
</html>
